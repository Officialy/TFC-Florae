buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

if (System.getenv('VERSION') != null) {
    version = System.getenv('VERSION')
} else {
    version = "1.1.1"
}
group = "tfcflorae"
archivesBaseName = "TFC-Florae"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"
    mappings = "stable_39"
	
	replace("@VERSION@", project.version)
	replace("@FINGERPRINT@", project.findProperty("signSHA1"))
    replaceIn("TFCFlorae.java")
}

repositories {
    // Repositories required for dependencies, not ForgeGradle go here.
    mavenCentral()
	maven { // TOP
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
	maven {
		name "Craftweaker"
        url "https://maven.blamejared.com"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "CoFH Maven"
        url = "https://maven.covers1624.net/"
    }
    maven {
		url "https://minecraft.curseforge.com/api/maven"
	}
}

dependencies {
	deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.615"
    deobfProvided "mezz.jei:jei_1.12.2:4.16.1.302"
	compile files("./libs/TerraFirmaCraft-MC1.12.2-1.7.11.169-deobf.jar")
    compile fileTree(include: ["*.jar"], dir: "libs")
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
	
	// from the project root, include the LICENSE file
    from('.') {
        include "LICENSE.txt"
    }
}

project.tasks.withType(Jar) { jarTask -> // For all jar tasks
    jarTask.appendix = "MC" + project.minecraft.version
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf { // Skip the task if our secret data isn't available
        project.hasProperty('keyStore')
    }

    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar